buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url artifactoryURL
            credentials {
                username = artifactoryUser
                password = artifactoryPassword
            }
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

apply plugin: com.github.jengelman.gradle.plugins.shadow.ShadowPlugin

configurations {
    cglibDep
}

dependencies {
    cglibDep 'cglib:cglib:3.2.0'
}

def packagesToRelocate = ['javassist', 'org.glassfish', 'org.jvnet', 'jersey.repackaged',
                          'com.fasterxml', 'io.netty', 'org.bouncycastle', 'org.apache',
                          'org.aopalliance', 'org.scijava', 'com.google', 'javax.annotation',
                          'javax.inject', 'javax.ws', 'net.sf', 'org.objectweb']

shadowJar {
    from rootProject.configurations.findByName('dockerJava')
    mergeServiceFiles()
    def basePackage = "com.github.dockerjava.shaded"
    packagesToRelocate.each { pkg ->
        relocate pkg, "${basePackage}.${pkg}"
    }
    // Keep this uber JAR free of java 9 and java 11 modules for now
    exclude 'module-info.class'
    exclude 'META-INF/versions/**'
}

jar {
    dependsOn shadowJar
    duplicatesStrategy = 'exclude'
    manifest {
        attributes 'Implementation-Title': "${project.name}",
                   'Implementation-Version': "${->project.version}",
                   'Built-By': System.getProperty('user.name'),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty('java.version'),
                   'Built-Gradle': gradle.gradleVersion
    }
    from zipTree(shadowJar.outputs.files.singleFile)
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task docsJar(type: Jar, dependsOn: javadoc) {
    classifier 'docs'
    from javadoc.destinationDir
}

task cglibJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar, dependsOn: jar) {
    classifier 'cglib'
    from zipTree(jar.outputs.files.singleFile)
    configurations = [project.configurations.cglibDep]
    def basePackage = "com.github.dockerjava.shaded"
    packagesToRelocate.each { pkg ->
        relocate pkg, "${basePackage}.${pkg}".toString()
    }
}
